10 rem ****** 3D Y-axis Rotation Code with depth fading *****
20 rem ****** By Martin Rait 1993 *****
30 rem               Uses Misty Extension
40 rem
50 rem ** Version One - Fast with (Amiga Simulated) Flicker **
60 rem ** Version Two - Slow without Flicker **
70 rem
80 rem --> If you have any suggestions phone me (04)475-9025 <--
90 rem --> or post to PO BOX 27-025 WELLINGTON NEW ZEALAND <--
100 rem
110 mode 0 : key off : curs off : hide 
120 palette $0,$700,$70,$7,$444,$555,$666,$777,$700
130 rem initial YA for calcs should be 90. Reset below accordingly
140 ICR=15
150 ANG=10
160 NS=24
170 FACES=18 : FACES1=17
180 IYA=90-ICR : YA=IYA
190 logic=back : rem version two
200 rem auto back off : rem version one
210 rem ** Load Star Data into Object Array **
220 dim OBJ#(NS,2),VW#(NS,2),FACE(FACES,3),PX(NS),PY(NS),PZ#(NS),IDER#(FACES1),IDER2#(FACES1)
230 for RD=1 to NS
240 for DR=0 to 2
250 read OBJ#(RD,DR)
260 next DR : next RD
270 for RD=1 to FACES
280 for DR=0 to 3
290 read FACE(RD,DR)
300 next DR : next RD
310 rem
320 rem ** Transfer Star Data into View Array **
330 for I=1 to NS
340 VW#(I,0)=OBJ#(I,0)
350 VW#(I,1)=OBJ#(I,1)
360 VW#(I,2)=OBJ#(I,2)
370 next I
380 gosub 450
390 gosub 1220
400 gosub 900
410 gosub 1220
420 gosub 1060
430 gosub 1220
440 goto 380
450 rem ** Angle Incrementation **
460 for LOP=1 to ANG
470 YA=YA+ICR
480 if YA>360 then YA=1
490 rem ** Calculations **
500 RYA#=rad(YA)
510 SN#=sin(RYA#) : CS#=cos(RYA#)
520 for I=1 to NS
530 X#=OBJ#(I,0) : Z#=OBJ#(I,2)
540 NX#=X#*SN#-Z#*CS#
550 Z#=X#*CS#+Z#*SN#
560 VW#(I,0)=NX# : VW#(I,2)=Z#
570 next I
580 gosub 610
590 next LOP
600 return 
610 rem ** Display Star Map **
620 cls
630 for I=1 to NS
640 PX(I)=(160+(VW#(I,0)*2))
650 PY(I)=(100-(VW#(I,1)*2))
660 PZ#(I)=VW#(I,2)
670 next I
680 for I=1 to FACES
690 ZF#=PZ#(FACE(I,0))+PZ#(FACE(I,1))+PZ#(FACE(I,2))+PZ#(FACE(I,3))
700 IDER#(I-1)=ZF# : IDER2#(I-1)=ZF#
710 next I
720 sort IDER2#(0)
730 for I=0 to FACES1
740 for N=0 to FACES1
750 if IDER2#(I)=IDER#(N) then IDER2#(I)=N+1
760 next N
770 next I
780 for I=1 to FACES
790 FUM=IDER2#(I-1)
800 X1=PX(FACE(FUM,0)) : Y1=PY(FACE(FUM,0))
810 X2=PX(FACE(FUM,1)) : Y2=PY(FACE(FUM,1))
820 X3=PX(FACE(FUM,2)) : Y3=PY(FACE(FUM,2))
830 X4=PX(FACE(FUM,3)) : Y4=PY(FACE(FUM,3))
835 if FUM>14 then ink FUM-10 else ink FUM
840 polygon X1,Y1 to X2,Y2 to X3,Y3 to X4,Y4 to X1,Y1
850 rem ink 8 : draw X1,Y1 to X2,Y2 : draw to X3,Y3 : draw to X1,Y1
860 next I
870 screen swap : rem version two
880 wait vbl 
890 return 
900 rem ** Angle Incrementation **
910 for LOP=1 to ANG
920 YA=YA+ICR
930 if YA>360 then YA=1
940 rem ** Calculations **
950 RYA#=rad(YA)
960 SN#=sin(RYA#) : CS#=cos(RYA#)
970 for I=1 to NS
980 Y#=OBJ#(I,1) : Z#=OBJ#(I,2)
990 NY#=Y#*SN#-Z#*CS#
1000 Z#=Y#*CS#+Z#*SN#
1010 VW#(I,1)=NY# : VW#(I,2)=Z#
1020 next I
1030 gosub 610
1040 next LOP
1050 return 
1060 rem ** Angle Incrementation **
1070 for LOP=1 to ANG
1080 YA=YA+ICR
1090 if YA>360 then YA=1
1100 rem ** Calculations **
1110 RYA#=rad(YA)
1120 SN#=sin(RYA#) : CS#=cos(RYA#)
1130 for I=1 to NS
1140 X#=OBJ#(I,0) : Y#=OBJ#(I,1)
1150 NX#=X#*SN#-Y#*CS#
1160 Y#=X#*CS#+Y#*SN#
1170 VW#(I,0)=NX# : VW#(I,1)=Y#
1180 next I
1190 gosub 610
1200 next LOP
1210 return 
1220 rem ******* subroutine  *****
1230 for I=1 to NS
1240 OBJ#(I,0)=VW#(I,0)
1250 OBJ#(I,1)=VW#(I,1)
1260 OBJ#(I,2)=VW#(I,2)
1270 next I
1280 YA=IYA
1290 return 
1300 rem ** point data**
1310 data-20,5,10
1320 data-20,5,-10
1330 data 20,5,10
1340 data 20,5,-10
1350 data-20,-5,10
1360 data-20,-5,-10
1370 data 20,-5,10
1380 data 20,-5,-10
1390 data 0,12,6
1400 data 0,12,-6
1410 data 15,12,6
1420 data 15,12,-6
1430 data 0,5,6
1440 data 0,5,-6
1450 data 15,5,6
1460 data 15,5,-6
1461 data-20,9,1
1462 data-20,9,-1
1463 data 0,9,1
1464 data 0,9,-1
1465 data-20,7,1
1466 data-20,7,-1
1467 data 0,7,1
1468 data 0,7,-1
1470 rem line data
1480 data 1,2,6,5
1490 data 3,4,8,7
1500 data 1,2,4,3
1510 data 5,6,8,7
1520 data 1,3,7,5
1530 data 2,4,8,6
1540 data 9,10,14,13
1550 data 11,12,16,15
1560 data 9,10,12,11
1570 data 13,14,16,15
1580 data 9,11,15,13
1590 data 10,12,16,14
1600 data 17,18,22,21
1610 data 19,20,24,23
1620 data 17,18,20,19
1630 data 21,22,24,23
1640 data 17,19,23,21
1650 data 18,20,24,22
