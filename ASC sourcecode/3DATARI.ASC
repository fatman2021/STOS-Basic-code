1 rem ****** 3D Y-axis Rotation Code with depth fading *****
2 rem ****** By Martin Rait 1993 *****
3 rem               Uses Misty Extension
4 rem
5 rem ** Version One - Fast with (Amiga Simulated) Flicker **
6 rem ** Version Two - Slow without Flicker **
7 rem
8 rem --> If you have any suggestions phone me (04)475-9025 <--
9 rem --> or post to PO BOX 27-025 WELLINGTON NEW ZEALAND <--
10 rem
15 mode 0 : key off : curs off : hide 
20 palette $0,$1,$2,$3,$4,$5,$6,$7,$700
25 rem initial YA for calcs should be 90. Reset below accordingly
30 YA=80
31 ANG=10
35 NS=84
36 ND=42
40 logic=back : rem version two
41 rem auto back off : rem version one
45 rem ** Load Star Data into Object Array **
50 dim OBJ#(NS,3),VW#(NS,3)
70 for RD=1 to ND
71 for DR=0 to 2
72 read OBJ#(RD,DR)
78 next DR : next RD
79 rem ****
80 for RD=1 to ND
81 for DR=0 to 2
82 OBJ#(ND+RD,DR)=OBJ#(RD,DR)
83 if DR=2 then OBJ#(ND+RD,DR)=2
88 next DR : next RD
89 rem ****
99 rem ****
100 rem
200 rem ** Transfer Star Data into View Array **
210 for I=1 to NS
220 VW#(I,0)=OBJ#(I,0)
230 VW#(I,1)=OBJ#(I,1)
240 VW#(I,2)=OBJ#(I,2)
250 next I
260 gosub 300
261 gosub 800
262 gosub 600
263 gosub 800
264 gosub 700
265 gosub 800
270 goto 260
300 rem ** Angle Incrementation **
310 for LOP=1 to ANG
320 YA=YA+10
330 if YA>360 then YA=1
400 rem ** Calculations **
410 RYA#=rad(YA)
420 SN#=sin(RYA#) : CS#=cos(RYA#)
430 for I=1 to NS
440 X#=OBJ#(I,0) : Z#=OBJ#(I,2)
450 NX#=X#*SN#-Z#*CS#
460 Z#=X#*CS#+Z#*SN#
470 VW#(I,0)=NX# : VW#(I,2)=Z#
475 next I
480 gosub 500
481 next LOP
490 return 
500 rem ** Display Star Map **
505 cls
510 for I=1 to NS
520 X=(160+(VW#(I,0)*4))
530 Y=(100-(VW#(I,1)*4))
535 Z=VW#(I,2)
536 CON=(Z)+50
537 C=(CON/20)+2
540 extension #N logic,X,Y,C
550 next I
551 screen swap : rem version two
555 wait vbl 
560 return 
600 rem ** Angle Incrementation **
605 for LOP=1 to ANG
610 YA=YA+10
620 if YA>360 then YA=1
630 rem ** Calculations **
640 RYA#=rad(YA)
650 SN#=sin(RYA#) : CS#=cos(RYA#)
660 for I=1 to NS
670 Y#=OBJ#(I,1) : Z#=OBJ#(I,2)
680 NY#=Y#*SN#-Z#*CS#
685 Z#=Y#*CS#+Z#*SN#
690 VW#(I,1)=NY# : VW#(I,2)=Z#
691 next I
695 gosub 500
696 next LOP
697 return 
700 rem ** Angle Incrementation **
705 for LOP=1 to ANG
710 YA=YA+10
720 if YA>360 then YA=1
730 rem ** Calculations **
740 RYA#=rad(YA)
750 SN#=sin(RYA#) : CS#=cos(RYA#)
760 for I=1 to NS
770 X#=OBJ#(I,0) : Y#=OBJ#(I,1)
780 NX#=X#*SN#-Y#*CS#
785 Y#=X#*CS#+Y#*SN#
790 VW#(I,0)=NX# : VW#(I,1)=Y#
791 next I
795 gosub 500
796 next LOP
797 return 
800 rem ******* subroutine  *****
805 for I=1 to NS
810 OBJ#(I,0)=VW#(I,0)
820 OBJ#(I,1)=VW#(I,1)
830 OBJ#(I,2)=VW#(I,2)
840 next I
845 YA=80
850 return 
9999 rem ** Star Data **
10000 data-2,15,-2
10010 data-2,10,-2
10020 data-2,5,-2
10030 data-2,0,-2
10040 data-2,-5,-2
10050 data-2,-10,-2
10060 data-2,-15,-2
10070 data-5,15,-2
10080 data-5,10,-2
10090 data-5,5,-2
10100 data-7,0,-2
10110 data-8,-5,-2
10120 data-10,-10,-2
10130 data-14,-15,-2
10140 data-8,15,-2
10150 data-8,10,-2
10160 data-9,5,-2
10170 data-11,0,-2
10180 data-12,-5,-2
10190 data-15,-10,-2
10200 data-19,-15,-2
20000 data 2,15,-2
20010 data 2,10,-2
20020 data 2,5,-2
20030 data 2,0,-2
20040 data 2,-5,-2
20050 data 2,-10,-2
20060 data 2,-15,-2
20070 data 5,15,-2
20080 data 5,10,-2
20090 data 5,5,-2
20100 data 7,0,-2
20110 data 8,-5,-2
20120 data 10,-10,-2
20130 data 14,-15,-2
20140 data 8,15,-2
20150 data 8,10,-2
20160 data 9,5,-2
20170 data 11,0,-2
20180 data 12,-5,-2
20190 data 15,-10,-2
20200 data 19,-15,-2
